buildscript {
    repositories {
        jcenter()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '4.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
forgeVersion = "1.16.5"

minecraft {
    mappings channel: 'official', version: '1.16.5'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven { url 'https://jitpack.io' }

    flatDir {
        dirs '../Libraries/'
    }

    ivy {
        setUrl('https://download.nodecdn.net/containers/reforged/server/release')
        metadataSources {
            artifact()
        }
        patternLayout {
            artifact('[revision]/[artifact].[ext]')
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.34'
    implementation 'pixelmon:Pixelmon-1.16.5-9.0.8a-server:9.0.8'

    shadow group: 'org.spongepowered', name: 'configurate-yaml', version: '4.0.0'

    shadow (group: 'com.github.EnvyWare.API', name: 'commons', version: '3.5.4')
    shadow (group: 'com.github.EnvyWare.API', name: 'forge16', version: '3.5.4') {
        transitive = false
    }
    shadow (group: 'com.github.EnvyWare.API', name: 'reforged16', version: '3.5.4') {
        transitive = false
    }

    shadow (project(':api')) {
        transitive = false
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    archiveName = rootProject.name + '-Forge.jar'

    relocate('org.spongepowered.configurate', 'com.envyful.pixel.hunt.remastered.forge.shade.configurate')
    relocate('org.yaml.snakeyaml', 'com.envyful.pixel.hunt.remastered.forge.shade.snakeyaml')
    relocate('io.leangen.geantyref', 'com.envyful.pixel.hunt.remastered.forge.shade.geantyref')
    relocate('com.google.gson', 'com.envyful.pixel.hunt.remastered.forge.shade.gson')
    relocate('com.zaxxer', 'com.envyful.pixel.hunt.remastered.forge.shade.hikari')
    relocate('org.slf4j', 'com.envyful.pixel.hunt.remastered.forge.shade.slf4j')
    relocate('com.envyful.api', 'com.envyful.pixel.hunt.remastered.forge.shade.envy.api')
    relocate('org.bstats', 'com.envyful.pixel.hunt.remastered.forge.shade.bstats')


    exclude "**/module-info.class"
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

jar.finalizedBy('reobfJar')
build.finalizedBy('versionedRelease')