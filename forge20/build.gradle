plugins {
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id("com.github.johnrengelman.shadow") version "8.1.1"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

forgeVersion = "1.20.1"

minecraft {
    mappings channel: 'official', version: '1.20.1'
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        name = 'spongepowered-repo'
        url = 'https://repo.spongepowered.org/maven'
    }

    maven {
        url "https://maven.envyware.co.uk/releases"
    }

    ivy {
        setUrl('https://download.nodecdn.net/containers/reforged/server/release')
        metadataSources {
            artifact()
        }
        patternLayout {
            artifact('[revision]/[artifact].[ext]')
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:1.20.1-47.1.44"

    implementation 'pixelmon:Pixelmon-1.20.1-9.2.0-server:9.2.0'

    implementation group: 'org.spongepowered', name: 'configurate-yaml', version: '4.0.0'

    shadow group: 'com.envyful.api', name: 'commons', version: '5.0.1'
    shadow (group: 'com.envyful.api', name: 'forge20', version: '5.0.1') {
        transitive = false;
    }
    shadow (group: 'com.envyful.api', name: 'reforged20', version: '5.0.1') {
        transitive = false;
    }
}

shadowJar {
    finalizedBy('reobfJar')
    configurations = [project.configurations.shadow]
    zip64 = true
    setArchiveClassifier('')
    setArchiveBaseName(rootProject.name + "-Forge")

    relocate('org.spongepowered.configurate', 'com.envyful.pixel.hunt.remastered.forge.shade.configurate')
    relocate('org.yaml.snakeyaml', 'com.envyful.pixel.hunt.remastered.forge.shade.snakeyaml')
    relocate('io.leangen.geantyref', 'com.envyful.pixel.hunt.remastered.forge.shade.geantyref')
    relocate('com.google.gson', 'com.envyful.pixel.hunt.remastered.forge.shade.gson')
    relocate('com.zaxxer', 'com.envyful.pixel.hunt.remastered.forge.shade.hikari')
    relocate('org.slf4j', 'com.envyful.pixel.hunt.remastered.forge.shade.slf4j')
    relocate('com.envyful.api', 'com.envyful.pixel.hunt.remastered.forge.shade.envy.api')
    relocate('org.bstats', 'com.envyful.pixel.hunt.remastered.forge.shade.bstats')


    exclude "**/module-info.class"
}

jar.finalizedBy('shadowJar')
build.finalizedBy('versionedRelease')